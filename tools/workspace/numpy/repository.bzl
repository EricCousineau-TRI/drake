# -*- mode: python -*-
# vi: set ft=python :

"""
Provides NumPy from a wheel file, or provides a no-op to pass NumPy through
from the system.

Example:
    WORKSPACE:
        load("@drake//tools/workspace/numpy:repo.bzl", "numpy_repository")
        numpy_repository(
            name = "foo",
        )

    BUILD:
        py_library(
            name = "foobar",
            deps = ["@foo//:numpy"],
            srcs = ["bar.py"],
        )

Arguments:
    name: A unique name for this rule.
    use_local: Use a locally installed version of NumPy. Use at your own risk!
"""

load("@drake//tools/workspace:os.bzl", "determine_os")

# Generated from Git revision 7d247f4 from numpy/numpy#10898.
# See `./experimental/README.md` for instructions to generate these binaries.
# PR DRAFT(eric.cousineau): Upload these to S3 or whatevs if/when they pass
# review.
wheels = {
    "ubuntu_1604": {
        "url": "https://github.com/EricCousineau-TRI/experimental/raw/e84664659a7ceeac016f626b4d77f5a89c4cad62/numpy/numpy-1.15.0.dev0%2B7d247f4-cp27-cp27mu-linux_x86_64.whl",  # noqa
        "sha256": "643f7e11c5f0213ae171d91e5759da51e4f39a0a9ba912f42189b3004507c21c",  # noqa
    },
    "mac": {
        "url": "https://github.com/EricCousineau-TRI/experimental/raw/3211d6dd6aee9e8dd70a19ef86b961a2de3bf821/numpy/numpy-1.15.0.dev0%2B7d247f4-cp27-cp27m-macosx_10_13_x86_64.whl",  # noqa
        "sha256": "1b3cee66ff92e2e0dc1c97d167b5b13873c3e97a87829a1c7830964ab48b2648",  # noqa
    },
}

def _impl(repository_ctx):
    # Do not name this `numpy`, as Bazel will make PYTHONPATH look for love in
    # all the wrong places. If `numpy` is located in the repo root,
    # `random` will be shadowed by `numpy.random`, causing everything to fail.
    # If installed elsewhere, Bazel will put its silly `__init__.py` and leak
    # the wrong path, which will shadow the real `numpy`.
    # See https://github.com/bazelbuild/bazel/issues/3998
    if repository_ctx.name == "numpy":
        fail("Do not name this repository `numpy`. Please name it " +
             "`numpy_py` or something else.")

    os_result = determine_os(repository_ctx)
    if os_result.error != None:
        fail(os_result.error)

    wheel = None
    if os_result.is_macos:
        wheel = wheels["mac"]
    elif os_result.is_ubuntu:
        if os_result.ubuntu_release == "16.04":
            wheel = wheels["ubuntu_1604"]
    if wheel == None:
        fail("Unsupported platform")

    repository_ctx.download_and_extract(
        url = wheel["url"],
        sha256 = wheel["sha256"],
        type = "zip",
    )

    file_content = """# -*- python -*-

# DO NOT EDIT: generated by numpy_repository()

load("@drake//tools/install:install.bzl", "install")

licenses([
    "notice",  # BSD-2-Clause AND BSD-3-Clause AND MIT AND Python-2.0
    "unencumbered",  # Public-Domain
])

# Interpret `numpy` sources as data to simplify handling.
filegroup(
    name = "data",
    srcs = glob(["numpy/**/*"]),
    visibility = ["//visibility:private"],
)

py_library(
    name = "numpy_py",
    imports = ["."],
    visibility = ["//visibility:public"],
    data = [":data"],
)

install(
    name = "install",
    data = [":data"],
    data_dest = "lib/python2.7/site-packages",
    visibility = ["//visibility:public"],
)
    """

    repository_ctx.file("BUILD.bazel", content = file_content,
                        executable = False)

_numpy_repository = repository_rule(
    _impl,
)

def _fake_impl(repository_ctx):
    repository_ctx.file(
        "BUILD.bazel",
        content = repository_ctx.attr.build_file_content,
        executable = False)

_fake_repository = repository_rule(
    attrs = {
        "build_file_content": attr.string(mandatory = True),
    },
    implementation = _fake_impl,
)

def numpy_repository(
        name = None,
        use_local = False):
    if name == None:
        fail("Must supply name")
    if not use_local:
        _numpy_repository(name = name)
    else:
        _fake_repository(
            name = name,
            build_file_content = """
load("@drake//tools/install:install.bzl", "install")

py_library(
    name = "numpy_py",
    visibility = ["//visibility:public"],
)

install(
    name = "install",
    visibility = ["//visibility:public"],
)
"""
        )
