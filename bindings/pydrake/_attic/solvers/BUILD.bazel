# -*- python -*-

load("@drake//tools/install:install.bzl", "install")
load("//tools/lint:lint.bzl", "add_lint_tests")
load(
    "@drake//tools/skylark:test_tags.bzl",
    "gurobi_test_tags",
    "mosek_test_tags",
)
load(
    "@drake//tools/skylark:pybind.bzl",
    "drake_pybind_library",
    "get_drake_py_installs",
    "get_pybind_package_info",
)
load(
    "@drake//tools/skylark:drake_py.bzl",
    "drake_py_library",
    "drake_py_test",
    "drake_py_unittest",
)

package(default_visibility = [
    "//bindings/pydrake:__subpackages__",
])

# This determines how `PYTHONPATH` is configured, and how to install the
# bindings.
PACKAGE_INFO = get_pybind_package_info("//bindings")

drake_py_library(
    name = "module_py",
    srcs = ["__init__.py"],
    imports = PACKAGE_INFO.py_imports,
    deps = [
        "//bindings/pydrake:module_py",
    ],
)

# TODO(eric.cousineau): Move this into `multibody`.
drake_pybind_library(
    name = "ik_py",
    cc_deps = ["//bindings/pydrake:documentation_pybind"],
    cc_srcs = ["ik_py.cc"],
    package_info = PACKAGE_INFO,
    py_deps = [
        "//bindings/pydrake/multibody:rigid_body_tree_py",
    ],
    py_srcs = ["_ik_extra.py"],
)

PY_LIBRARIES_WITH_INSTALL = [
    ":ik_py",
]

PY_LIBRARIES = [
    ":module_py",
]

# Symbol roll-up (for user ease).
drake_py_library(
    name = "all_py",
    srcs = ["all.py"],
    deps = PY_LIBRARIES_WITH_INSTALL + PY_LIBRARIES,
)

# Package roll-up (for Bazel dependencies).
drake_py_library(
    name = "solvers",
    deps = [":all_py"],
)

install(
    name = "install",
    targets = PY_LIBRARIES + [":all_py"],
    py_dest = PACKAGE_INFO.py_dest,
    deps = get_drake_py_installs(PY_LIBRARIES_WITH_INSTALL),
)

drake_py_test(
    name = "pr2_ik_test",
    # TODO(jwnimmer-tri) Port this to drake_py_unittest.
    allow_import_unittest = True,
    data = ["//examples/pr2:models"],
    tags = ["snopt"],
    # Use non-attic for testing.
    deps = ["//bindings/pydrake/solvers:ik_py"],
)

drake_py_unittest(
    name = "rbt_ik_test",
    data = ["//examples/pendulum:models"],
    tags = ["snopt"],
    # Use non-attic for testing.
    deps = ["//bindings/pydrake/solvers:ik_py"],
)

add_lint_tests(
    cpplint_data = ["//bindings/pydrake:.clang-format"],
    enable_clang_format_lint = True,
)
