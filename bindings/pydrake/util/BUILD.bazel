# Backwards-compatibility aliases.

load("@drake//tools/install:install.bzl", "install")
load("//tools/lint:lint.bzl", "add_lint_tests")
load(
    "@drake//tools/skylark:drake_cc.bzl",
    "drake_cc_googletest",
)
load(
    "//tools/skylark:drake_py.bzl",
    "drake_py_library",
    "drake_py_test",
)
load(
    "//tools/skylark:pybind.bzl",
    "get_pybind_package_info",
)
load(":build_macros.bzl", "util_cc_header_alias", "util_target_aliases")

package(default_visibility = [
    "//bindings/pydrake:__subpackages__",
])

# This determines how `PYTHONPATH` is configured, and how to install the
# bindings.
PACKAGE_INFO = get_pybind_package_info("//bindings")

drake_py_library(
    name = "module_py",
    srcs = [
        "__init__.py",
        "all.py",
    ],
    deps = [
        "//bindings/pydrake/common",
    ],
)

# Package roll-up (for Bazel dependencies).
drake_py_library(
    name = "util",
    imports = PACKAGE_INFO.py_imports,
    deps = [":module_py"],
)

PY_LIBRARIES = [
    ":module_py",
]

cc_header_alias_names = [
    "cpp_param_pybind",
    "cpp_template_pybind",
    "deprecation_pybind",
    "drake_optional_pybind",
    "eigen_geometry_pybind",
    "eigen_pybind",
    "type_pack",
    "type_safe_index_pybind",
    "wrap_function",
    "wrap_pybind",
]

[
    util_cc_header_alias(name = name)
    for name in cc_header_alias_names
]

# Add target aliases for remaining Python libraries.
util_target_aliases(
    names = [
        "compatibility_py",
        "deprecation_py",
        "eigen_geometry_py",
        "cpp_template_py",
        "cpp_const_py",
        "cpp_param_py",
        "pybind11_version_py",
    ],
)

install(
    name = "install",
    targets = PY_LIBRARIES + cc_header_alias_names,
    py_dest = PACKAGE_INFO.py_dest,
)

drake_cc_googletest(
    name = "alias_cc_test",
    deps = cc_header_alias_names + [
        "//:drake_shared_library",
    ],
)

drake_py_test(
    name = "alias_py_test",
    allow_import_unittest = True,
    deps = ["util"],
)

add_lint_tests()
